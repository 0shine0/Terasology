/*
 * This is a Gradle build file:
 * - Gradle Homepage: http://gradle.org/
 * - Gradle Documentation: http://gradle.org/documentation
 * - View tasks for this project: $ gradlew tasks
 */

apply plugin: 'project-report'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'

/* Gradle Wrapper ---------------------------------------------------------- */
/* Uncomment this task and change 'gradleVersion' to create a new wrapper version.
task wrapper(type: Wrapper) {
    gradleVersion = '1.1'
}
*/

/* General: Java/Grooy ----------------------------------------------------- */
sourceCompatibility = 1.6

// Compiles everything as groovy, due to java<->groovy interdependencies
compileJava.enabled = false
sourceSets.main.groovy.srcDirs = ["src/main/java", "src/main/groovy"]

// Workaround for task "run"
// TODO find a way to remove this and the bad workaround for JARs
sourceSets.main.output.resourcesDir = sourceSets.main.output.classesDir

sourceSets {
    perfTest
}

/* Dependencies/Configurations --------------------------------------------- */
configurations.compile.transitive = false;

configurations {
    perfTestCompile.extendsFrom testCompile
}

repositories {
    mavenCentral()
}

dependencies {
    groovy group: 'org.codehaus.groovy', name: 'groovy', version: '1.8.7'
    compile group: 'com.google.guava', name: 'guava', version: '12.0.1'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.1'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '2.4.1'
    compile group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.2'
    compile group: 'com.projectdarkstar.ext.jorbis', name: 'jorbis', version: '0.0.17'
    compile group: 'org.lwjgl.lwjgl', name: 'lwjgl', version: '2.8.3'
    compile group: 'org.lwjgl.lwjgl', name: 'lwjgl_util', version: '2.8.3'
    compile group: 'io.netty', name: 'netty', version: '3.4.4.Final'
    compile group: 'org.reflections', name: 'reflections', version: '0.9.7.RC1'
    compile group: 'java3d', name: 'vecmath', version: '1.3.1'
    compile fileTree(dir: 'libs', include: '*.jar')
    testCompile group: 'junit', name: 'junit', version: '4.10'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.0'
    perfTestCompile sourceSets.main.output // TODO Check why this is needed.
}

/* Natives ----------------------------------------------------------------- */
task extractWindowsNatives(type:Copy) {
    from {
        configurations.runtime.collect { it.getName().contains('-natives-window') ? zipTree(it) : [] }
    }
    into ('natives/windows')
    exclude ('META-INF/**')
}

task extractMacOSXNatives(type:Copy) {
    from {
        configurations.runtime.collect { it.getName().contains('-natives-osx') ? zipTree(it) : [] }
    }
    into ('natives/macosx')
    exclude ('META-INF/**')
}

task extractLinuxNatives(type:Copy) {
    from {
        configurations.runtime.collect { it.getName().contains('-natives-linux') ? zipTree(it) : [] }
    }
    into ('natives/linux')
    exclude ('META-INF/**')
}

task extractNatives {
    dependsOn extractWindowsNatives
    dependsOn extractLinuxNatives
    dependsOn extractMacOSXNatives
}

/* IDE --------------------------------------------------------------------- */
ideaModule.dependsOn extractNatives
eclipseClasspath.dependsOn extractNatives

/* Tasks ------------------------------------------------------------------- */
jar {
    manifest {
// def manifestClasspath = configurations.runtime.collect { it.getName() }.join(',')
// def manifestClasspath = 'libs/'+configurations.runtime.collect { it.getName() }.join(' libs/') 
        attributes("Implementation-Title": project.name, "Main-Class": "org.terasology.game.Terasology")
    }
}
jar.doFirst {
    sourceSets.main.output.resourcesDir = '/dev/null'
}
jar.doLast {
    sourceSets.main.output.resourcesDir = sourceSets.main.output.classesDir
}

test {
    // ignoreFailures: Specifies whether the build should break when the verifications performed by this task fail.
    ignoreFailures = true
    // showStandardStreams: makes the standard streams (err and out) visible at console when running tests
    testLogging.showStandardStreams = true
    jvmArgs '-Xms512m', '-Xmx1024m', '-XX:MaxPermSize=128m'
}

// TODO This is not a normal JUnit Test. It should be replaced with a JavaExec task.
task perfTests(type: Test) {
    dependsOn cleanPerfTests
    testLogging.showStandardStreams = true
    testClassesDir = sourceSets.perfTest.output.classesDir
    classpath = sourceSets.perfTest.runtimeClasspath
    jvmArgs '-Xms512m', '-Xmx1024m', '-XX:MaxPermSize=128m'
}

task run(type:JavaExec) {
    description = 'Run Terasology'
    dependsOn extractNatives
    dependsOn classes
    main = "org.terasology.game.Terasology"
    classpath sourceSets.main.output.classesDir
    classpath configurations.runtime
}
